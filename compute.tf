locals {
  czs = data.google_compute_zones.available.names
}

data "google_compute_zones" "available" {}

resource "google_compute_instance" "toggl_compute_instance" {
  count        = 2
  name         = "toggl-compute-instance-${count.index + 1}"
  machine_type = var.instance_type
  zone         = local.czs[0]

  network_interface {
    network    = google_compute_network.toggl_network.name
    subnetwork = google_compute_subnetwork.toggl_subnet.name

    access_config {
      // Ephemeral Public IP
    }
  }

  boot_disk {
    initialize_params {
      image = var.instance_image
    }
  }

  metadata = {
    ssh-keys = "${var.ssh_user}:${file(var.public_key_path)}"
  }
}

resource "local_file" "gcp_hosts_output" {
  depends_on = [google_compute_instance.toggl_compute_instance]
  
  content = <<-DOC
    [master]
    ${google_compute_instance.toggl_compute_instance[0].network_interface.0.access_config.0.nat_ip}
    
    [replica]
    ${google_compute_instance.toggl_compute_instance[1].network_interface.0.access_config.0.nat_ip}
    DOC
  
  filename = "./files/gcp_hosts.ini"
}

resource "local_file" "ansible_vars_output" {
  depends_on = [google_compute_instance.toggl_compute_instance, google_storage_bucket.toggl_backup_bucket]
  
  content = <<-DOC
    # Ansible variables.yml File Generated by Terraform
    ---
    instance_1: ${google_compute_instance.toggl_compute_instance[0].name}
    instance_2: ${google_compute_instance.toggl_compute_instance[1].name}
    
    private_ip_1: ${google_compute_instance.toggl_compute_instance[0].network_interface.0.network_ip}
    private_ip_2: ${google_compute_instance.toggl_compute_instance[1].network_interface.0.network_ip}
    
    public_ip_1: ${google_compute_instance.toggl_compute_instance[0].network_interface.0.access_config.0.nat_ip}
    public_ip_2: ${google_compute_instance.toggl_compute_instance[1].network_interface.0.access_config.0.nat_ip}
    
    bucket_url: ${google_storage_bucket.toggl_backup_bucket.url}
    
    pg_base_path: /var/lib/pgsql/15
    pg_data_path: /var/lib/pgsql/15/data
    pg_bin_path: /usr/pgsql-15/bin
    credentials_file: ../files/credentials.json
    DOC
  
  filename = "./playbooks/variables.yml"
}